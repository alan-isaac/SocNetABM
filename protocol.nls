; the reporters for the behaviorspace run


; reports true if scientists have converged and have become 
; sufficiently confident in their belief
to-report exit-condition
  ifelse converged [
    let min-confidence [confidence] of min-one-of turtles [confidence]
    ifelse min-confidence > confidence-cutoff [
      report true
    ][
      report false
    ]
  ][
    report false
  ]
end





; reports true if all scientists converged to one theory, and immediately
; calculates their confidence
to-report converged
  let any-th1-scientists any? turtles with [mytheory = 0]
  let any-th2-scientists any? turtles with [mytheory = 1]
  ifelse any-th1-scientists and any-th2-scientists [
    set converged-ticks 0    
    report false
  ][
    ifelse any-th1-scientists [
      if last-converged-th = 1 [
        set converged-ticks 0
      ]
      set last-converged-th 0
    ][
      if last-converged-th = 0 [
        set converged-ticks 0
      ]
      set last-converged-th 1
    ]
    set converged-ticks converged-ticks + 1
    calc-confidence
    report true
  ]
end





; reports 1 if scientists converged on the best theory (th1) by the end of the 
; run, 0 otherwise
to-report successful-run
  ifelse count turtles with [mytheory = 0] = scientists [
    report 1
  ][
    report 0
  ]
end





; reports the number of researchers on th# at the end of the run
to-report run-end-scientists [th#]
  if th# = "th1" [
    set th# 0
  ]
  if th# = "th2" [
    set th# 1
  ]
  report count turtles with [mytheory = th#]
end





to-report average-jumps
  let jumps 0
  let avgjumps 0
  ask turtles [
    set jumps times-jumped + jumps
  ]
  set avgjumps (jumps / scientists)
  report avgjumps
end





; reports the average proportion of time the researchers considered both 
; theories equally good. The +1 accounts for the extra calculation already 
; taking place during the setup
to-report avg-indiff-time
  report (indiff-count / (scientists * (ticks + 1)))
end





; reports the last time-point scientists converged. The + 1 correction is due 
; to the way how `converged-ticks` is set in the `converged` procedure.
to-report round-converged
  report ticks - converged-ticks + 1
end





to-report average-signal [th#]
  if critical-interaction and th-i-signal = (list th1-signal th2-signal) [
    set th-i-signal [0 0]
    ask turtles [
      set th-i-signal (map + th-i-signal subj-th-i-signal) 
    ]
    set th-i-signal map [ cur-th-signal -> cur-th-signal / scientists]
      th-i-signal
  ]
  if th# = "th1" [
    set th# 0
  ]
  if th# = "th2" [
    set th# 1
  ]
  report item th# th-i-signal
end

